plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'net.researchgate.release' version '3.1.0'
}

ext {
    // Project constants
    github_org = 'Discord4J'
    project_name = 'stores'
    artifact_group = 'com.discord4j'
    project_version = "$version".toString()
    project_description = 'Java interface for the Discord API'
    project_jdk = '1.8'
    jdk = JavaVersion.current().majorVersion
    jdk_javadoc = "https://docs.oracle.com/javase/$jdk/docs/api/".toString()
    if (JavaVersion.current().isJava11Compatible()) {
        jdk_javadoc = "https://docs.oracle.com/en/java/javase/$jdk/docs/api/".toString()
    }

    // Dependencies
    reactor_bom_version = '2023.0.8'
    servicer_version = '1.0.3'
    junit_version = '5.7.1'
    logback_version = '1.3.14'

    caffeine_version = '2.9.3' // TODO: Version 3.x require JDK 11
    lettuce_version = '6.3.2.RELEASE'
    jackson_version = '2.17.2'
    commons_lang_version = '3.15.0'
    testcontainers_version = '1.20.0'
    assertj_version = '3.26.3'

    isJitpack = "true" == System.getenv("JITPACK")
    isRelease = !version.toString().endsWith('-SNAPSHOT')
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'maven-publish'
    if (!isJitpack && isRelease) {
        apply plugin: 'signing'
    }

    group = artifact_group
    version = project_version
    description = project_description

    java {
        sourceCompatibility = project_jdk
        targetCompatibility = project_jdk
    }

    dependencies {
        api platform("io.projectreactor:reactor-bom:$reactor_bom_version")

        compileOnly "com.google.code.findbugs:jsr305:3.0.1"
        testCompileOnly "com.google.code.findbugs:jsr305:3.0.1"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        if (!isRelease || isJitpack) {
            maven { url = 'https://central.sonatype.com/repository/maven-snapshots' }
            maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
            maven { url = 'https://oss.sonatype.org/content/repositories/staging' }
        }
        maven { url = 'https://jitpack.io' }
        maven { url = 'https://repo.spring.io/milestone' }
    }

    javadoc {
        options {
            encoding = 'UTF-8'
            tags = ["apiNote:a:API Note:",
                    "implSpec:a:Implementation Requirements:",
                    "implNote:a:Implementation Note:"]
            addStringOption 'Xdoclint:none', '-quiet'
            addStringOption 'encoding', 'UTF-8'
            links = [
                    jdk_javadoc,
                    "https://www.reactive-streams.org/reactive-streams-1.0.4-javadoc/",
                    "https://projectreactor.io/docs/core/release/api/",
            ]
        }
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.incremental = true
    }

    tasks.register('downloadDependencies') {
        description 'Download all dependencies to the Gradle cache'
        doLast {
            configurations.findAll { it.canBeResolved }.files
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }
}

subprojects {

    base {
        archivesName = 'stores-' + project.name
    }

    tasks.withType(Javadoc).configureEach {
        title = "${base.archivesName.get()} ${version} API"
        options.windowTitle = "${base.archivesName.get()} ($version)"
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                afterEvaluate {
                    artifactId = jar.archiveBaseName.get()
                }

                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }

                pom {
                    name = 'Stores'
                    description = 'A set of pre-made store implementations for Discord4J'
                    url = 'https://github.com/Discord4J/Stores'
                    organization {
                        name = 'Discord4J'
                        url = 'https://discord4j.com'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/Discord4J/Stores/issues'
                    }
                    licenses {
                        license {
                            name = 'LGPL-3.0'
                            url = 'https://github.com/Discord4J/Stores/LICENSE.txt'
                            distribution = 'repo'
                        }
                    }
                    scm {
                        url = 'https://github.com/Discord4J/Stores'
                        connection = 'scm:git:git://github.com/Discord4J/Stores.git'
                        developerConnection = 'scm:git:ssh://git@github.com:Discord4J/Stores.git'
                    }
                    developers {
                        developer {
                            name = 'The Discord4J Team'
                        }
                    }
                }
            }
        }

        if (!isJitpack) {
            repositories {
                maven {
                    if (isRelease) {
                        url = 'https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2'
                    } else {
                        url = 'https://central.sonatype.com/repository/maven-snapshots'
                    }
                    def sonatypeUsername = findProperty('sonatypeUsername')
                    def sonatypePassword = findProperty('sonatypePassword')
                    if (sonatypeUsername != null && sonatypePassword != null) {
                        credentials {
                            username sonatypeUsername
                            password sonatypePassword
                        }
                    }
                }
            }
        }
    }

    if (!isJitpack && isRelease) {
        signing {
            def signingKey = findProperty("signingKey")
            def signingPassword = findProperty("signingPassword")
            if (signingKey != null && signingPassword != null) {
                useInMemoryPgpKeys(new String(signingKey.decodeBase64()), signingPassword as String)
            }
            sign publishing.publications.mavenJava
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            def sonatypeUsername = findProperty('sonatypeUsername')
            def sonatypePassword = findProperty('sonatypePassword')
            if (sonatypeUsername != null && sonatypePassword != null) {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }
}

release {
    preTagCommitMessage = 'Release version'
    tagCommitMessage = 'Release version'
    newVersionCommitMessage = 'Next development version'
    git {
        requireBranch.set('')
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

def buildDocsUrl(String project) {
    if (isJitpack) {
        def docVersion = System.getenv('GIT_COMMIT')
        return "https://javadoc.jitpack.io/com/discord4j/stores/$project/$docVersion/javadoc/" as String
    } else {
        def docVersion = isRelease ? "$version" : "3.0"
        return "https://www.javadoc.io/page/com.discord4j/$project/$docVersion/" as String
    }
}
